    SpriteSet
	grass          > Immovable img=oryx/grass autotiling=True hidden=True
	highway        > Immovable img=newset/highway hidden=True
	forestDense    > SpawnPoint stype=log prob=0.4 cooldown=10 img=newset/water5
	forestSparse   > SpawnPoint stype=log prob=0.1 cooldown=5 img=newset/water5
	water          > Immovable color=BLUE img=newset/water2_1
	goal           > Immovable color=GREEN img=newset/exit2
	log            > Missile orientation=LEFT speed=0.1 color=BROWN img=newset/log
	fastRtruck     > Missile orientation=RIGHT speed=0.2 color=ORANGE img=newset/car3R
	slowRtruck     > Missile orientation=RIGHT speed=0.1 color=RED img=newset/car4R
	fastLtruck     > Missile orientation=LEFT speed=0.2 color=ORANGE img=newset/car3
	slowLtruck     > Missile orientation=LEFT speed=0.1 color=RED img=newset/car4
	avatar         > MovingAvatar img=newset/girl1
	wall           > Immovable color=BLACK img=oryx/tree2

        
    InteractionSet
	goal  avatar        > killSprite scoreChange=1
	avatar  log         > changeResource resource=safety value=2
	avatar  log         > pullWithIt
	avatar  wall        > stepBack
	avatar  EOS         > stepBack
	avatar  water       > killIfHasLess resource=safety limit=-1
	avatar  water       > changeResource resource=safety value=-1
	log  EOS            > wrapAround
	avatar  fastRtruck  > killSprite
	avatar  slowRtruck  > killSprite
	avatar  fastLtruck  > killSprite
	avatar  slowLtruck  > killSprite
	slowRtruck  EOS     > wrapAround
	fastRtruck  EOS     > wrapAround
	slowLtruck  EOS     > wrapAround
	fastLtruck  EOS     > wrapAround

    
    TerminationSet
        SpriteCounter stype=goal   limit=0 win=True
        SpriteCounter stype=avatar limit=0 win=False
    
    LevelMapping
        g > grass goal
        0 > water grass
        1 > forestDense water       # note how a single character can spawn multiple sprites
        2 > forestDense log
        3 > forestSparse water       # note how a single character can spawn multiple sprites
        4 > forestSparse log
        - > highway slowRtruck
        x > highway fastRtruck
        _ > highway slowLtruck
        l > highway fastLtruck
        = > log water
        B > avatar log water
        A > avatar grass
        w > wall grass
        + > grass
        . > highway grass

    LevelDescription
	wwwwwwwwwwwwwwwwwwwwwwwwwwww
	+++++++++++w  g  w++++++++++++
	000==000000===0000=====000=2
	00000====0000000000====00012
	000===000===000====0000===02
	www+++ww+++www++++www++wwwww
	....----...---...-..----....
	.-.....xx.......xxx....xx..
	..-...---.....-...----.--...
	w+++++++A++++++++++++++++++w
	wwwwwwwwwwwwwwwwwwwwwwwwwwww